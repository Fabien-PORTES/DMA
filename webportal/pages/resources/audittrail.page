import "/webportal/pages/resources/concepts_nls.page"
import "/webportal/pages/resources/mappings.page"

entityDetailsManagerAuditDialog_NLS = NLS {
	title [en "Audit trail" fr "Audit des modifications"]
	^when [en "when" fr "quand"]
	^what [en "what" fr "quoi"]
	^who [en "who" fr "qui"]
	^status [en "current status" fr "Ã©tat d'avancement"]
}

entityDetailsManagerTicketTypeMapping = StringMapping {
	when "Account" then "ACCOUNTMANAGER"
	when "Application" then "APPLICATIONMANAGER"
	when "Permission" then "PERMISSIONMANAGER"
	when "Group" then "GROUPMANAGER"
	when "Repository" then "REPOSITORYMANAGER"
}

/**
 * Used to show a management audit trail.
 * 
 * @param type is the type of ticket, can be Account, Application, Permission, Group, Repository
 * @param entity is the corresponding entity uid (Account, Application, Permission, Group, Repository)
 * @param rawtype can be used instead of type if the type has to be overriden by a facet (for instance for sharepoint elements, file share elements, ...)
 */
entityDetailsManagerAuditDialog = Dialog {
	title:$entityDetailsManagerAuditDialog_NLS.title
	width:1024
	height:400
	buttons:OkButton
	enter-event:StringCase(rawtype) { when Not IsEmpty then [Set rawtype to internalType] otherwise [Set Transform type using entityDetailsManagerTicketTypeMapping to internalType] }
	
	entity = Variable
	type = Variable // Account, Application, Permission, Group, Repository
	rawtype = Variable // ACCOUNTMANAGER, APPLICATIONMANAGER, PERMISSIONMANAGER, GROUPMANAGER, REPOSITORYMANAGER
	
	internalType = Variable
	
	audittrail = Dataset {
		view:br_manager_audittrail with entity to entityuid, internalType to tickettype
	}

	Table {
		data:audittrail
		layout:Layout{grab:horizontal True vertical True}
		configurable:True
		show-filter:True
		
		Column {column:status width:96px header:$entityDetailsManagerAuditDialog_NLS.^status sortable:True filterable:True}
		// when = close date when finalized, submission date when it is still in progress
		Column {column: closedate width:128px header:$entityDetailsManagerAuditDialog_NLS.^when sortable:True text:$utilsNLS.showifnotnull(Transform Current closedate using notnullMapping, Current closedate, Current submissiondate)}
		Column {column:actorhrcode initially-masked:True header:$pidentity.hrcode sortable:True filterable:True}
		Column {column:actorfullname width:160px header:$entityDetailsManagerAuditDialog_NLS.^who sortable:True filterable:True}
		Column {column:actormail width:160px initially-masked:True header:$pidentity.mail sortable:True filterable:True}
		Column {column:comment width:1024px header:$entityDetailsManagerAuditDialog_NLS.^what sortable:True filterable:True}
	}

}

