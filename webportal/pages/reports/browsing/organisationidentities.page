import "/webportal/pages/styles/styles.page"
import "/webportal/pages/resources/mappings.page"
import "/webportal/pages/resources/pages_nls.page"
import "/webportal/pages/reports/standard.page"

reportDetails = NLS {
	browsing_organisation_identities.pagetitle [ en "Members of the Organisation" fr "Membres de l'Organisation" ]
	browsing_organisation_identities.title [ en "Members of the Organisation" fr "Membres de l'Organisation" ]
	browsing_organisation_identities.subtitle [ en "Identities - Organisation <<{0}>>" fr "Identités - Organisation <<{0}>>" ]
	browsing_organisation_identities.columns.hrcode [ en "Identity HR code" fr "Matricule de l'identité" ]
	browsing_organisation_identities.columns.fullname [ en "Full name" fr "Nom complet" ]
	browsing_organisation_identities.columns.internal [ en "Internal user" fr "Personne interne" ]
	browsing_organisation_identities.columns.jobtitledisplayname [ en "Job title" fr "Métier" ]
	browsing_organisation_identities.directonly [ en "Direct people only" fr "Personnes directes uniquement"]
}

browsing_organisation_identities = Page {

	title: $reportDetails.browsing_organisation_identities.pagetitle
	tags: "organisation" priority 200, "byuid" , "browsing"
	icon: "16/details/browsing_16.png" large-icon: "48/details/browsing_48.png"
	feature: browsing_organisation_identities
	enter-event: BooleanCase (paramSearchMode) { when False then [ Set uid to nonEmptyUid] when True then []}
	
	mixins: simpleReport
	
	uid = Parameter {
		label:"Concept Uid"
		type:String
	}
	paramDirectonly = Parameter {
		type: Boolean 
		mandatory: False
		default: False
		multivalued: False
	}

	// Related to Search Navigator	
	paramSearchMode = Parameter {
		type: Boolean
		hidden: True
		default: False
	}
	// Related to Search Navigator	
	organisationSearch = SearchDefinition {
		entity: Organisation
		simple-mode-rule: br_organisation_simple_search
	}
	
	// Related to Search Navigator
	currentOrganisation = Variable {
		type: String
		initial: uid
	}
	
	selectedUid = Variable {
		type: String
	}
	
	selectedRecorduid = Variable {
		type: Integer
	}
	directOnly = Variable {
		type: Boolean
		multivalued: False
		initial: paramDirectonly
	}
	
	// Empty uid when search mode
	nonEmptyUid = Variable { type: String }
	
	// Related to Search Navigator	
	organisationList = Dataset {
		search: Search {
			definition: organisationSearch
			view: br_organisation_searchpoint with nonEmptyUid to uid
		}
	}
	
	conceptData = Record {
		view: br_organisationDetail with currentOrganisation to uid
	}
	
	dataToReport = Dataset {
		view: br_identityinorganisation with conceptData.recorduid to orgrecorduid, directOnly to directonly
	}

	Group{
		layout: Layout { grab: horizontal False vertical False align: horizontal Center vertical Beginning }
		Spacer {
			height: 11
			hidden: Not BooleanPredicate(paramSearchMode)
		}
		SetNavigator {
			data: organisationList
			column: uid
			selection: currentOrganisation
			back-link: Activity Organisation Detail with currentOrganisation to paramOrganisationUid , SerializeSearch organisationSearch to organisationSearch, paramSearchMode to paramSearchMode
			initial: uid
			show-count: True
			show-first: True
			show-last: True
			count-width: 150
			hidden: Not BooleanPredicate(paramSearchMode)
		}
	}
		
	Group {
		Text {
			value: $reportDetails.browsing_organisation_identities.title	
		}		
		
		Link {
			icon: "32/details/printable_32_gray.png"
			target: Report browsing_organisationidentities with conceptData.recorduid to recorduid
			value: ""
			compact: True 
			layout: Layout { grab: horizontal False vertical False  indent: horizontal 0 vertical 0  align: horizontal End vertical Center}
			align: Right 
		}
		
		Text {
			value: $reportDetails.browsing_organisation_identities.subtitle( conceptData.displayname )
			styling: reportSubtitle
			align: Left
		}
		Checkbox {
			styling: reportSubtitle
			layout: Layout { align: horizontal End vertical Beginning }
			variable: directOnly
			text: $reportDetails.browsing_organisation_identities.directonly
		}
		
		Table{
			data: dataToReport
			double-click: GoTo Activity Identity Detail with selectedUid to paramIdentityUid
			show-filter: True
			show-count: True
			
			Column {
				column: recorduid
				hidden: True
				selection: selectedRecorduid
			}
			Column {
				column: uid
				hidden: True
				selection: selectedUid
			}
			Column{
				column: hrcode
				header: $reportDetails.browsing_organisation_identities.columns.hrcode
				sortable: True
				filterable: True
				width:100%
				align: Center
			}
			Column{
				column: fullname
				header: $reportDetails.browsing_organisation_identities.columns.fullname
				sortable: True
				filterable: True
				width:100%
			}
			Column{
				column: internal
				text: Transform Current internal using booleanDisplayMapping
				header: $reportDetails.browsing_organisation_identities.columns.internal
				sortable: True
				filterable: True
				width:100%
				align: Center
			}
			Column{
				column: jobtitledisplayname
				header: $reportDetails.browsing_organisation_identities.columns.jobtitledisplayname
				sortable: True
				filterable: True
				width:100%
			}
		}
		
		Button {
			actions:  GoTo Activity Identity Detail with selectedUid to paramIdentityUid
			layout: Layout { align: horizontal End vertical Beginning grab: horizontal False vertical False hint: width 200}
			disabled: StringPredicate(selectedUid) { when IsEmpty then True }
			text: $search_page.showdetails
		}
	}
}