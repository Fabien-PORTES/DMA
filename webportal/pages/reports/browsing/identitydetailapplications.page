import "/webportal/pages/styles/styles.page"
import "/webportal/pages/resources/mappings.page"
import "/webportal/pages/resources/pages_nls.page"

reportDetails = NLS {
	browsing_identity_detailapplications.pagetitle [ en "Applications Details" fr "Applications Détaillées"]
	browsing_identity_detailapplications.title [ en "List of Applications" fr "Liste des Applications"]
	browsing_identity_detailapplications.subtitle [ en "Applications - Identity <<{0}>>" fr "Applications - Identité <<{0}>>"]
	browsing_identity_detailapplications.columns.code [ en "Code" fr "Code"]
	browsing_identity_detailapplications.columns.displayname [en "Name" fr "Nom"]
	browsing_identity_detailapplications.columns.repositorydisplayname [ en "Repository" fr "Référentiel"]
	browsing_identity_detailapplications.columns.repositorycode [ en "Repository Code" fr "Code du Référentiel"]
}

browsing_identity_detailapplications = Page {

	title: $reportDetails.browsing_identity_detailapplications.pagetitle
	tags: "identity" priority 200, "byuid" , "browsing"
	icon: "16/details/browsing_16.png" large-icon: "48/details/browsing_48.png"
	feature: browsing_identity_detailapplications
	enter-event: BooleanCase (paramSearchMode) { when False then [ Set uid to nonEmptyUid] when True then []}
	
	mixins: simpleReport
	
	uid = Parameter {
		label:"Concept Uid"
		type:String
	}

	// Related to Search Navigator	
	paramSearchMode = Parameter {
		type: Boolean
		hidden: True
		default: False
	}
	// Related to Search Navigator	
	identitySearch = SearchDefinition {
		entity: Identity
		simple-mode-rule: br_identity_simple_search
	}
	
	// Related to Search Navigator
	currentIdentity = Variable {
		type: String
		initial: uid
	}
	
	selectedUid = Variable {
		type: String
	}
	
	selectedRecorduid = Variable {
		type: Integer
	}
	
	// Empty uid when search mode
	nonEmptyUid = Variable { type: String }
	
	// Related to Search Navigator	
	identityList = Dataset {
		search: Search {
			definition: identitySearch
			view: br_identity_searchpoint with nonEmptyUid to uid
		}
	}
	
	conceptData = Record {
		view: br_identityDetail with currentIdentity to uid
	}
	
	dataToReport = Dataset {
		view: br_applicationsbyidentitysimple with conceptData.recorduid to identityrecorduid
	}

	Group{
		layout: Layout { grab: horizontal False vertical False align: horizontal Center vertical Beginning }
		Spacer {
			height: 11
			hidden: Not BooleanPredicate(paramSearchMode)
		}
		SetNavigator {
			data: identityList
			column: uid
			selection: currentIdentity
			back-link: Activity Identity Detail with currentIdentity to paramIdentityUid , SerializeSearch identitySearch to identitySearch, paramSearchMode to paramSearchMode
			initial: uid
			show-count: True
			show-first: True
			show-last: True
			count-width: 150
			hidden: Not BooleanPredicate(paramSearchMode)
		}
	}
		
	Group {
		Text {
			value: $reportDetails.browsing_identity_detailapplications.title	
		}		
		
		Text {
			value: $reportDetails.browsing_identity_detailapplications.subtitle( conceptData.fullname )
			styling: reportSubtitle
			align: Left
		}
		
		Table{
			data: dataToReport
			double-click: GoTo Activity Application Detail with selectedUid to paramApplicationUid
			show-filter: True
			show-count: True
			
			Column {
				column: recorduid
				hidden: True
				selection: selectedRecorduid
			}
			Column {
				column: uid
				hidden: True
				selection: selectedUid
			}
			Column{
				column: code
				header: $reportDetails.browsing_identity_detailapplications.columns.code
				sortable: True
				filterable: True
				width:100%
			}
			Column{
				column: displayname
				header: $reportDetails.browsing_identity_detailapplications.columns.displayname
				sortable: True
				filterable: True
				width:100%
			}
			Column{
				column: repositorydisplayname
				header: $reportDetails.browsing_identity_detailapplications.columns.repositorydisplayname
				sortable: True
				filterable: True
				width:100%
			}
			Column{
				column: repositorycode
				header: $reportDetails.browsing_identity_detailapplications.columns.repositorycode
				sortable: True
				filterable: True
				width:100%
			}
		}
		
		Button {
			actions:  GoTo Activity Application Detail with selectedUid to paramApplicationUid
			layout: Layout { align: horizontal End vertical Beginning grab: horizontal False vertical False hint: width 200}
			disabled: StringPredicate(selectedUid) { when IsEmpty then True }
			text: $search_page.showdetails
		}
	}
}