import "/webportal/pages/styles/styles.page"
import "/webportal/pages/resources/mappings.page"
import "/webportal/pages/resources/pages_nls.page"
import "/webportal/pages/reports/standard.page"

reportDetails = NLS {
	browsing_account_allgroups.pagetitle [ en "Account Groups" fr "Groupes du Compte" ]
	browsing_account_allgroups.title [ en "Account Groups" fr "Groupes du Compte" ]
	browsing_account_allgroups.subtitle [ en "Groups - Account <<{0}>>" fr "Groupes - Compte <<{0}>>" ]
	browsing_account_allgroups.columns.group_displayname [ en "Group display name" fr "Nom d'affichage du groupe" ]
	browsing_account_allgroups.columns.group_comment [ en "Group comment" fr "Commentaire du groupe" ]
}

browsing_account_allgroups = Page {

	title: $reportDetails.browsing_account_allgroups.pagetitle
	tags: "account" priority 200, "byuid" , "browsing"
	icon: "16/details/browsing_16.png" large-icon: "48/details/browsing_48.png"
	feature: browsing_account_allgroups
	enter-event: BooleanCase (paramSearchMode) { when False then [ Set uid to nonEmptyUid] when True then []}
	
	mixins: simpleReport
	
	uid = Parameter {
		label:"Concept Uid"
		type:String
	}

	// Related to Search Navigator	
	paramSearchMode = Parameter {
		type: Boolean
		hidden: True
		default: False
	}
	// Related to Search Navigator	
	accountSearch = SearchDefinition {
		entity: Account
		simple-mode-rule: br_account_simple_search
	}
	
	// Related to Search Navigator
	currentAccount = Variable {
		type: String
		initial: uid
	}
	
	selectedUid = Variable {
		type: String
	}
	
	selectedRecorduid = Variable {
		type: Integer
	}
	
	// Empty uid when search mode
	nonEmptyUid = Variable { type: String }
	
	// Related to Search Navigator	
	accountList = Dataset {
		search: Search {
			definition: accountSearch
			view: br_account_searchpoint with nonEmptyUid to uid
		}
	}
	
	conceptData = Record {
		view: br_accountDetail with currentAccount to uid
	}
	
	dataToReport = Dataset {
		view: br_accountallgroups with conceptData.recorduid to accountrecorduid
	}

	Group{
		layout: Layout { grab: horizontal False vertical False align: horizontal Center vertical Beginning }
		Spacer {
			height: 11
			hidden: Not BooleanPredicate(paramSearchMode)
		}
		SetNavigator {
			data: accountList
			column: uid
			selection: currentAccount
			back-link: Activity Account Detail with currentAccount to paramAccountUid , SerializeSearch accountSearch to accountSearch, paramSearchMode to paramSearchMode
			initial: uid
			show-count: True
			show-first: True
			show-last: True
			count-width: 150
			hidden: Not BooleanPredicate(paramSearchMode)
		}
	}
		
	Group {
		Text {
			value: $reportDetails.browsing_account_allgroups.title	
		}		
		
		Link {
			icon: "32/details/printable_32_gray.png"
			target: Report browsing_accountallgroups with conceptData.recorduid to recorduid
			value: ""
			compact: True 
			layout: Layout { grab: horizontal False vertical False  indent: horizontal 0 vertical 0  align: horizontal End vertical Center}
			align: Right 
		}
		
		Text {
			value: $reportDetails.browsing_account_allgroups.subtitle( conceptData.login )
			styling: reportSubtitle
			align: Left
		}
		
		Table{
			data: dataToReport
			double-click: GoTo Activity Group Detail with selectedUid to paramGroupUid
			show-filter: True
			show-count: True
			
			Column {
				column: group_recorduid
				hidden: True
				selection: selectedRecorduid
			}
			Column {
				column: group_uid
				hidden: True
				selection: selectedUid
			}
			Column{
				column: group_displayname
				header: $reportDetails.browsing_account_allgroups.columns.group_displayname
				sortable: True
				filterable: True
				width:100%
			}
			Column{
				column: group_comment
				header: $reportDetails.browsing_account_allgroups.columns.group_comment
				sortable: True
				filterable: True
				width:100%
			}
		}
		
		Button {
			actions:  GoTo Activity Group Detail with selectedUid to paramGroupUid
			layout: Layout { align: horizontal End vertical Beginning grab: horizontal False vertical False hint: width 200}
			disabled: StringPredicate(selectedUid) { when IsEmpty then True }
			text: $search_page.showdetails
		}
	}
}