import "/webportal/pages/styles/styles.page"
import "/webportal/pages/resources/mappings.page"
import "/webportal/pages/resources/pages_nls.page"
import "/webportal/pages/reports/standard.page"

reportDetails = NLS {
	browsing_account_usages.pagetitle [ en "Account Usages" fr "Usages du Compte" ]
	browsing_account_usages.title [ en "Account Usages" fr "Usages du Compte" ]
	browsing_account_usages.subtitle [ en "Usages - Account <<{0}>>" fr "Usages - Compte <<{0}>>" ]
	browsing_account_usages.columns.identityfullname [ en "Identity full name" fr "Nom complet de l'identit√©" ]
	browsing_account_usages.columns.applicationdisplayname [ en "Application display name" fr "Nom d'affichage de l'application" ]
	browsing_account_usages.columns.permissiondisplayname [ en "Permission display name" fr "Nom d'affichage de la permission" ]
	browsing_account_usages.columns.counter [ en "Usage count" fr "Compteur d'usage" ]
}

browsing_account_usages = Page {

	title: $reportDetails.browsing_account_usages.pagetitle
	tags: "account" priority 200, "byuid" , "browsing"
	icon: "16/details/browsing_16.png" large-icon: "48/details/browsing_48.png"
	feature: browsing_account_usages
	enter-event: BooleanCase (paramSearchMode) { when False then [ Set uid to nonEmptyUid] when True then []}
	
	mixins: simpleReport
	
	uid = Parameter {
		label:"Concept Uid"
		type:String
	}

	// Related to Search Navigator	
	paramSearchMode = Parameter {
		type: Boolean
		hidden: True
		default: False
	}
	// Related to Search Navigator	
	accountSearch = SearchDefinition {
		entity: Account
		simple-mode-rule: br_account_simple_search
	}
	
	// Related to Search Navigator
	currentAccount = Variable {
		type: String
		initial: uid
	}
	
	selectedUid = Variable {
		type: String
	}
	
	selectedRecorduid = Variable {
		type: Integer
	}
	
	// Empty uid when search mode
	nonEmptyUid = Variable { type: String }
	
	// Related to Search Navigator	
	accountList = Dataset {
		search: Search {
			definition: accountSearch
			view: br_account_searchpoint with nonEmptyUid to uid
		}
	}
	
	conceptData = Record {
		view: br_accountDetail with currentAccount to uid
	}
	
	dataToReport = Dataset {
		view: br_usage with conceptData.recorduid to accountrecorduid
	}

	Group{
		layout: Layout { grab: horizontal False vertical False align: horizontal Center vertical Beginning }
		Spacer {
			height: 11
			hidden: Not BooleanPredicate(paramSearchMode)
		}
		SetNavigator {
			data: accountList
			column: uid
			selection: currentAccount
			back-link: Activity Account Detail with currentAccount to paramAccountUid , SerializeSearch accountSearch to accountSearch, paramSearchMode to paramSearchMode
			initial: uid
			show-count: True
			show-first: True
			show-last: True
			count-width: 150
			hidden: Not BooleanPredicate(paramSearchMode)
		}
	}
		
	Group {
		Text {
			value: $reportDetails.browsing_account_usages.title	
		}		
		
		Link {
			icon: "32/details/printable_32_gray.png"
			target: Report browsing_accountusages with conceptData.recorduid to recorduid
			value: ""
			compact: True 
			layout: Layout { grab: horizontal False vertical False  indent: horizontal 0 vertical 0  align: horizontal End vertical Center}
			align: Right 
		}
		
		Text {
			value: $reportDetails.browsing_account_usages.subtitle( conceptData.login )
			styling: reportSubtitle
			align: Left
		}
		
		Table{
			data: dataToReport
			show-filter: True
			show-count: True
			
			Column{
				column: identityfullname
				header: $reportDetails.browsing_account_usages.columns.identityfullname
				sortable: True
				filterable: True
				width:100%
			}
			Column{
				column: applicationdisplayname
				header: $reportDetails.browsing_account_usages.columns.applicationdisplayname
				sortable: True
				filterable: True
				width:100%
			}
			Column{
				column: permissiondisplayname
				header: $reportDetails.browsing_account_usages.columns.permissiondisplayname
				sortable: True
				filterable: True
				width:100%
			}
			Column{
				column: counter
				header: $reportDetails.browsing_account_usages.columns.counter
				sortable: True
				filterable: True
				width:100%
				align: Center
			}
		}
		
	}
}